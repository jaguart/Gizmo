#!/usr/bin/env raku
use v6;

use Kaolin::File :find, :mv-file;

my %*SUB-MAIN-OPTS =
  :named-anywhere,    # named options anywhere
;

#-------------------------------------------------------------------------------
#| fix basenames to lowercase, alpha-numeric and hyphen only
multi sub MAIN (
    Str $dir = '.',
    Bool :$fix          = False,    #= rename files to fix basenames
    Bool :r($recursive) = False,    #= recurse into subfolders
    Bool :l($lc)        = True,     #= force filenames to lowercase
    Bool :v($verbose)   = False,    #= verbose
) {

    my $finder = $dir.IO but (Kaolin::File::Finder, Kaolin::File::Namer);

    my %count;
    for $finder.find(:$recursive) -> $f {
        if $f.basename eq $f.basename-clean {
            %count<ok>++;
            say 'ok: ', $f.basename if $verbose;
        }
        elsif $fix {
            %count<fixed>++;
            say 'fixed: ', $f.fix-basename.basename;
        }
        else {
            %count<bad>++;
            say 'bad: ', $f.basename, ' -> ', $f.basename-clean;
        }
    }
    say $*PROGRAM.basename, ': ', $finder.Str, ' ', %count;

}

#-------------------------------------------------------------------------------
